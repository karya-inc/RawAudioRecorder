// Create variables with empty default values
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''
ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.key"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    // Read local.properties file first if it exists
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each {name, value ->
        ext[name] = value
    }
} else {
    // Use system environment variables
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
    ext['signing.keyId'] = System.getenv('SIGNING_KEY_ID')
    ext['signing.password'] = System.getenv('SIGNING_PASSWORD')
    ext["signing.key"] = System.getenv('SIGNING_KEY')
}

// Set up Sonatype repository
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}


/*

Publish artifact to Staging Repositories: ./gradlew publishReleasePublicationToSonatypeRepository
From here either publish the artifact using the Sonatype UI itself (Close and then release the repository in Staging Repositories)
Or, ./gradlew closeAndReleaseSonatypeStagingRepository to publish from staging to production

OSSRH_USERNAME: https://s01.oss.sonatype.org/ username

OSSRH_PASSWORD: https://s01.oss.sonatype.org/ password

SONATYPE_STAGING_PROFILE_ID: Get this by clicking on Staging Profiles on the left pane of the Sonatype Account and then
selecting a profile in the right pane. Check the url in the address bar.
It should appear something like https://oss.sonatype.org/#stagingProfiles;721f909fe89978.
So 721f909fe89978 is the SONATYPE_STAGING_PROFILE_ID

SIGNING_KEY_ID: Last 8 digit of the gpg key (generally it is the title of the key which is something like CA925CD6C9E8D064FF05B4728190C4130ABA0F98 => 0ABA0F98)
REMEMBER: LAST 8 DIGIT ONLY, OTHERWISE PUBLISHING MIGHT FAIL

SIGNING_PASSWORD: GPG key paraphrase

SIGNING_KEY: Cryptographic key, can be exported using 'gpg --export-secret-keys <SIGNING_KEY_ID> | base64'

*/