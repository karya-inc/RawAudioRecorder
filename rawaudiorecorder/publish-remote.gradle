apply plugin: 'maven-publish'
apply plugin: 'signing'

def PUBLISH_GROUP_ID = 'io.github.karya-inc'
def PUBLISH_VERSION = '0.0.6'
def PUBLISH_ARTIFACT_ID = 'rawaudiorecorder'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifacts javaDocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "A lightweight Android library to record wave audio"
                    url = 'https://github.com/karya-inc/RawAudioRecorder.git'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'divyanshdxn@gmail'
                            name = 'divyanshdxn'
                            email = 'divyanshdxn@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:ssh://git@github.com/karya-inc/RawAudioRecorder.git'
                        developerConnection = 'scm:git:ssh://git@github.com/karya-inc/RawAudioRecorder.git'
                        url = 'https://github.com/karya-inc/RawAudioRecorder.git'
                    }
                }

            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}
